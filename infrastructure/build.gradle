buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:9.11.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.7.7'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.flywaydb.flyway' version '9.11.0'
}

group = 'tech.willeei.admin.catalogo.infrastructure'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


bootJar {
    archiveBaseName.set('application.jar')
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation 'io.vavr:vavr:0.10.4'

    implementation('mysql:mysql-connector-java')

    implementation('org.springdoc:springdoc-openapi-webmvc-core:1.7.0')
    implementation('org.springdoc:springdoc-openapi-ui:1.7.0')

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')

    testImplementation 'org.flywaydb:flyway-core'
    testImplementation('org.flywaydb:flyway-mysql')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation(project(path: ':domain', configuration: 'testClasses'))

    testImplementation('org.testcontainers:testcontainers:1.20.1')
    testImplementation('org.testcontainers:mysql:1.20.1')
    testImplementation('org.testcontainers:junit-jupiter:1.20.1')

    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_videos'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '9655'
    cleanDisabled = false
}

test {
    useJUnitPlatform()
}